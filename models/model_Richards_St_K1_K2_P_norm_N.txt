# JAGS model for estimating the number of total deaths from heat exhaustion and cliff falls at Raine Island

# The assumed cyclical nature of turtle deaths is modeled by the function
# provided in Girondot et al. (2007): Modeling approaches to quantify
# leatherback nesting trends in French Guiana and Suriname. Chelonian
# Conservation and Biology 6(1): 37-46.
#
# The function has the following form:
# S1 <- -S
# M1 <- (1 + (2 * exp(K) - 1) * exp((1/S1) * (P - d))) ^ (-1/exp(K))
# M2 <- (1 + (2 * exp(K) - 1) * exp((1/S) * (P - d))) ^ (-1/exp(K))
# N <- min + (max - min) * (M1 * M2)
#
# d is the number of days from the beginning of nesting season
# S > 0 and S1 = -S define the "fatness" of the function
# K > 0 defines the "flatness" at the peak of the function
# P defines where the peak is relatvie to the range of d min(d) < P < max(d).
# min = 0.
#
# Observed counts are assumed to be normally distributed around this function.
# 
# In this modified version, there are two K values: K1 and K2. The M1 and
# M2 above are modified as follows:
#
# M1 <- (1 + (2 * exp(K1) - 1) * exp((1/S1) * (P - d))) ^ (-1/exp(K1))
# M2 <- (1 + (2 * exp(K2) - 1) * exp((1/S) * (P - d))) ^ (-1/exp(K2))
#
# The parameter S is time dependent, whereas K is fixed over time.

# This version computes estimated deaths for the entire time series for each
# season.

model{

  for (i in 1:n.seasons){   # year
    for (j in 1:n.vec[i]){   # days
      # state
      M1[i, j] <- (1 + (2 * exp(K1) - 1) * exp((1/(-S[i])) * (P - t[i,j]))) ^ (-1/exp(K1))
      M2[i, j] <- (1 + (2 * exp(K2) - 1) * exp((1/S[i]) * (P - t[i,j]))) ^ (-1/exp(K2))
      X[i, j] <-  max[i] * (M1[i, j] * M2[i, j])
            
      # observation
      y[i, j] ~ dnorm(X[i, j], tau.y)
      loglik[i,j] <- logdensity.norm(y[i,j], X[i,j], tau.y)          
                      
    }
  }  


  for (i in 1:n.seasons){
     for (j in 1:max.days[i]){
        M1a[i, j] <- (1 + (2 * exp(K1) - 1) * exp((1/(-S[i])) * (P - j))) ^ (-1/exp(K1))
        M2a[i, j] <- (1 + (2 * exp(K2) - 1) * exp((1/S[i]) * (P - j))) ^ (-1/exp(K2))
        Xa[i, j] <-  max[i] * (M1a[i, j] * M2a[i, j])        
     }
     
     sumXa[i] <- round(sum(Xa[i,1:max.days[i]]))
     
     # Because of the large SEs in N and large uncertainties in sumXa, the proportion 
     # can get >> 1 when doing the math below. Somehow, sumXa needs to be constrained
     # to some value... That means X needs to have maximum... 
     
     prop[i] <- exp(log(sumXa[i]) - N[i]) 
     
     # convert this proportion to a random variable using some sort of 
     # distribution with sumXa and N as input. Beta is good...
     #prop[i] ~ dbeta(sumXa[i], N[i])

  }
  
  # priors
  for (yr in 1:n.seasons){
    max[yr] ~ dnorm(max.vec[yr], tau.max)T(0,)
    S[yr] ~ dnorm(0, 0.1)T(0,)
    N[yr] ~ dlnorm(N.mean.log[yr], tau.N[yr])T(, 15) # exp(15) = 3269017
    #prop[yr] ~ dbeta(1,1)
    tau.N[yr] <- 1/(N.SE.log[yr] * N.SE.log[yr])
  }

  K1 ~ dnorm(0, 0.01)T(0,)
  K2 ~ dnorm(0, 0.01)T(0,)
  P ~ dunif(10, 150)
  sigma.max ~ dgamma(5, 0.1)
  tau.max <- 1/(sigma.max * sigma.max)

  # observation SD is maxed out at 50. 
  sigma.y ~ dgamma(2, 0.1)T(, 50)
  tau.y <- 1/(sigma.y * sigma.y)

}
